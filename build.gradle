buildscript {
    repositories {
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
        jcenter()
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework:springloaded:1.2.8.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RC2")
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.2")
    }
}

plugins {
    id("org.jetbrains.kotlin.plugin.spring") version "1.2.21"
    id("org.jetbrains.kotlin.plugin.jpa") version "1.2.21"
    id("org.jetbrains.kotlin.jvm") version "1.2.21"
    id("io.spring.dependency-management") version "1.0.4.RELEASE"
    id("net.researchgate.release") version "2.6.0"

}

apply plugin: "org.springframework.boot"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "idea"

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/kotlin/main/")
    }
}

repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
    jcenter()
    maven { url "http://repo.spring.io/milestone" }
}

ext["kotlin.version"] = "1.2.21"
ext["coroutineVersion"] = "0.22.1"
ext["shiroVersion"] = "1.4.0"
dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jre8")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutineVersion")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-cache")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")
    compile("org.apache.shiro:shiro-spring-boot-web-starter:$shiroVersion")
    compile("org.apache.shiro:shiro-hazelcast:$shiroVersion")
    compile("com.hazelcast:hazelcast-hibernate52:1.2.2")
    compile("com.hazelcast:hazelcast-spring")
    compile("com.h2database:h2")
    compile("mysql:mysql-connector-java")
    //webjars
    compile("org.webjars:font-awesome:5.0.6")
    compile("org.webjars:jquery:3.1.1-1")
    compile("org.webjars:jquery-validation:1.17.0")
    compile("org.webjars:metisMenu:1.1.3-1")
    compile("org.webjars:bootstrap:3.3.7-1")
    compile("org.webjars:startbootstrap-sb-admin-2:3.3.7+1")
    compile("org.webjars:morrisjs:0.5.0")
    compile("org.webjars.bower:datatables-responsive:1.0.6")
    //wro4j
    compile("eu.michael-simons:wro4j-spring-boot-starter:0.3.10")
    testCompile("com.h2database:h2")
    testCompile("org.apache.ant:ant:1.10.1")
    testCompile("org.dbunit:dbunit:2.5.4")
    testCompile("org.junit.jupiter:junit-jupiter-api")
    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude module: "junit"
    }
    testRuntime("org.junit.jupiter:junit-jupiter-engine")
}

task copyToLib(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
    exclude '*spring-boot-devtools*.jar'
}

jar {
    enabled = true
    manifest {
        attributes(
                'Main-Class': 'org.appsugar.archetypes.ApplicationKt', 'Class-Path': configurations.runtime.collect {
            'lib/' + it.getName()
        }.join(' '))
    }
    //dependsOn copyToLib
}
bootJar { classifier = 'boot' }//set boot jar different name
bootRun { sourceResources sourceSets.main } //enable spring dev tools scan resource directory
kotlin { experimental.coroutines = 'enable' }//enable coroutine
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions { jvmTarget = "1.8" }
}
kapt {
    useBuildCache = true
}

junitPlatformTest {
    systemProperties["refreshDb"] = true
    systemProperties["spring.jpa.hibernate.ddl-auto"] = "create-drop"
}